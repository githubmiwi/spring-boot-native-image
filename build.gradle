plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.11.0'
}

group = 'sample'
description = 'spring-boot-native-image'
version = 'latest-25'

bootBuildImage {
    environment = [
            //"SOURCE_DATE_EPOCH": "${System.currentTimeMillis() / 1000 as long}",
            "BP_JVM_VERSION" : "25",
            //"JAVA_TOOL_OPTIONS": "",
            // https://github.com/paketo-buildpacks/native-image
            "BP_NATIVE_IMAGE" : "true",
            "BP_NATIVE_IMAGE_BUILD_ARGUMENTS": [
                    // (default) mostly-static linked executable
                    "-H:+UnlockExperimentalVMOptions -H:+StaticExecutableWithDynamicLibC",
                    // no fallback if native-image generation fails
                    "--no-fallback",
                    // optimize for host machine
                    "-march=native",
                    // Parallel GC: Garbage-First (G1) GC based on Java HotSpot VM
                    "--gc=parallel",
                    // min/max heap size for binary, tested by locally with VM options -Xms -Xmx
                    "-R:MinHeapSize=32m -R:MaxHeapSize=48m",
                    // -Os: optimizations except those that can increase code or image size significantly
                    "-Os"
            ].join(" "),
            "BP_BINARY_COMPRESSION_METHOD": "upx",
            "BP_RUNTIME_CERT_BINDING_DISABLED": "true"
    ]
    builder = "paketobuildpacks/builder-jammy-buildpackless-tiny"
    buildpacks = [
            "paketobuildpacks/upx:3.8.3",
            "paketobuildpacks/bellsoft-liberica:11.4.0",
            "paketobuildpacks/syft:2.21.0",
            "paketobuildpacks/executable-jar:6.13.3",
            "paketobuildpacks/spring-boot:5.33.4",
            "paketobuildpacks/java-native-image:12.0.0"
    ]
    runImage = "busybox:1.36.1-glibc"
    buildCache {
        bind {
            source.set("/tmp/paketobuildpacks/cache-${rootProject.name}.build")
        }
    }
    launchCache {
        bind {
            source.set("/tmp/paketobuildpacks/cache-${rootProject.name}.launch")
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    /*toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }*/
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.rednoise:rita:2.4.501'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}