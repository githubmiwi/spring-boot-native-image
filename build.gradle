plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.11.0'
}

group = 'sample'
description = 'spring-boot-native-image'
version = '0.4.0'

bootBuildImage {
    environment = [
            "BP_NATIVE_IMAGE" : "true",
            "BP_NATIVE_IMAGE_BUILD_ARGUMENTS": [
                    "-H:+StaticExecutableWithDynamicLibC",
                    "--no-fallback",
                    "--strict-image-heap",
                    "-march=native",
                    "-R:MinHeapSize=32m",
                    "-R:MaxHeapSize=48m",
                    "-O3" // best performance at the cost of longer build times
                    //'-J-Xmx2G'
            ].join(' '),
            "BP_BINARY_COMPRESSION_METHOD": "upx",
            "BP_JVM_VERSION" : "21"
    ]
    builder = "paketobuildpacks/builder-jammy-buildpackless-tiny"
    buildpacks = [
            "paketobuildpacks/java-native-image"
    ]
    runImage = "busybox:stable-glibc"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.rednoise:rita:2.4.501'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}